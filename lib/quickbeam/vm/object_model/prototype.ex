defmodule QuickBEAM.VM.ObjectModel.Prototype do
  @moduledoc "Shared JavaScript prototype access and chain helpers."

  import QuickBEAM.VM.Heap.Keys, only: [proto: 0, proxy_target: 0, proxy_handler: 0]

  alias QuickBEAM.VM.Builtin
  alias QuickBEAM.VM.Invocation
  alias QuickBEAM.VM.ObjectModel.Get
  alias QuickBEAM.VM.Execution.RegexpState
  alias QuickBEAM.VM.Heap

  def get({:obj, ref}) do
    case Heap.get_obj(ref, %{}) do
      {:qb_arr, _} -> array_like_prototype(ref)
      data when is_list(data) -> array_like_prototype(ref)
      map when is_map(map) and is_map_key(map, proxy_target()) -> proxy_prototype(map)
      map when is_map(map) -> object_map_prototype(ref, map)
      _ -> nil
    end
  end

  def get({:qb_arr, _}), do: Heap.get_func_proto()
  def get(value) when is_list(value), do: QuickBEAM.VM.Runtime.global_class_proto("Array")
  def get({:builtin, _, _} = callable), do: callable_prototype(callable)
  def get({:regexp, _, _}), do: QuickBEAM.VM.Runtime.global_class_proto("RegExp")

  def get({:regexp, _, _, ref}) do
    case RegexpState.fetch(ref, proto()) do
      {:ok, :undefined} -> QuickBEAM.VM.Runtime.global_class_proto("RegExp")
      {:ok, stored_proto} -> stored_proto
      :error -> QuickBEAM.VM.Runtime.global_class_proto("RegExp")
    end
  end

  def get({:closure, _, %QuickBEAM.VM.Function{} = function} = callable),
    do: function_kind_prototype(function, callable)

  def get({:bound, _, _, _, _} = callable), do: callable_prototype(callable)
  def get(%QuickBEAM.VM.Function{} = function), do: function_kind_prototype(function, function)
  def get(value) when is_function(value), do: Heap.get_func_proto()

  def get(value) when is_integer(value) or is_float(value),
    do: QuickBEAM.VM.Runtime.global_class_proto("Number")

  def get(value) when is_binary(value), do: QuickBEAM.VM.Runtime.global_class_proto("String")
  def get(value) when is_boolean(value), do: QuickBEAM.VM.Runtime.global_class_proto("Boolean")
  def get(_), do: nil

  def set({:obj, ref}, new_proto) do
    case Heap.get_obj(ref, %{}) do
      {:qb_arr, _} -> Heap.put_array_prop(ref, "__proto__", new_proto)
      data when is_list(data) -> Heap.put_array_prop(ref, "__proto__", new_proto)
      map when is_map(map) -> Heap.put_obj(ref, Map.put(map, proto(), new_proto))
      _ -> :ok
    end
  end

  def set(_, _), do: :ok

  def chain_contains?(value, target_ref),
    do: chain_contains?(get(value), target_ref, MapSet.new())

  def ordinary_chain_contains?({:obj, ref}, target_ref),
    do: ordinary_chain_contains?({:obj, ref}, target_ref, MapSet.new())

  def ordinary_chain_contains?(_, _target_ref), do: false

  defp ordinary_chain_contains?({:obj, ref}, target_ref, _seen) when ref == target_ref, do: true

  defp ordinary_chain_contains?({:obj, ref}, target_ref, seen) do
    if MapSet.member?(seen, ref) do
      false
    else
      case Heap.get_obj(ref, %{}) do
        map when is_map(map) and is_map_key(map, proxy_target()) ->
          false

        _ ->
          ordinary_chain_contains?(get({:obj, ref}), target_ref, MapSet.put(seen, ref))
      end
    end
  end

  defp ordinary_chain_contains?(_, _target_ref, _seen), do: false

  defp chain_contains?({:obj, ref}, target_ref, _seen) when ref == target_ref, do: true

  defp chain_contains?({:obj, ref} = object, target_ref, seen) do
    if MapSet.member?(seen, ref) do
      false
    else
      chain_contains?(get(object), target_ref, MapSet.put(seen, ref))
    end
  end

  defp chain_contains?(_, _target_ref, _seen), do: false

  defp proxy_prototype(map) do
    target = Map.fetch!(map, proxy_target())
    handler = Map.fetch!(map, proxy_handler())
    trap = Get.get(handler, "getPrototypeOf")

    if trap == :undefined or trap == nil do
      get(target)
    else
      Invocation.invoke_callback_or_throw(trap, [target])
    end
  end

  defp array_like_prototype(ref) do
    if Heap.get_array_prop(ref, "__arguments__") == true do
      Heap.get_object_prototype()
    else
      Heap.get_array_proto(ref)
    end
  end

  defp object_map_prototype(ref, map) do
    if Heap.get_object_prototype() == {:obj, ref} do
      nil
    else
      case object_map_prototype_value(ref, map) do
        :null_proto -> nil
        proto -> proto
      end
    end
  end

  defp object_map_prototype_value(ref, map) do
    cond do
      Map.has_key?(map, :__internal_proto__) -> Map.get(map, :__internal_proto__)
      array_prototype_map?(map) -> Heap.get_object_prototype()
      Heap.get_prop_desc(ref, proto()) -> Heap.get_object_prototype()
      true -> Map.get(map, proto(), Heap.get_object_prototype())
    end
  end

  defp array_prototype_map?(map) do
    Map.has_key?(map, "constructor") and Map.has_key?(map, "push") and Map.has_key?(map, "pop")
  end

  defp function_kind_prototype(%QuickBEAM.VM.Function{func_kind: 1}, _callable) do
    generator_function_prototype()
  end

  defp function_kind_prototype(%QuickBEAM.VM.Function{func_kind: 2}, _callable) do
    cached_function_kind_prototype(
      :qb_async_function_prototype,
      "AsyncFunction",
      &QuickBEAM.VM.Runtime.Globals.Constructors.async_function/2
    )
  end

  defp function_kind_prototype(%QuickBEAM.VM.Function{func_kind: 3}, _callable) do
    cached_function_kind_prototype(
      :qb_async_generator_function_prototype,
      "AsyncGeneratorFunction",
      &QuickBEAM.VM.Runtime.Globals.Constructors.async_generator_function/2
    )
  end

  defp function_kind_prototype(_function, callable), do: callable_prototype(callable)

  defp cached_function_kind_prototype(key, name, constructor) do
    case Process.get(key) do
      {:obj, _} = proto ->
        proto

      _ ->
        proto =
          Heap.wrap(%{
            "constructor" => {:builtin, name, constructor},
            {:symbol, "Symbol.toStringTag"} => name,
            proto() => Heap.get_func_proto()
          })

        Process.put(key, proto)
        proto
    end
  end

  defp generator_prototype_object do
    case Process.get(:qb_generator_prototype_object) do
      {:obj, _} = proto ->
        proto

      _ ->
        proto =
          Heap.wrap(%{
            proto() => QuickBEAM.VM.Runtime.global_class_proto("Iterator"),
            {:symbol, "Symbol.toStringTag"} => "Generator"
          })

        Process.put(:qb_generator_prototype_object, proto)
        proto
    end
  end

  defp generator_function_prototype do
    case Process.get(:qb_generator_function_prototype) do
      {:obj, _} = proto ->
        proto

      _ ->
        generator_proto = generator_prototype_object()

        proto =
          Heap.wrap(%{
            "constructor" =>
              {:builtin, "GeneratorFunction",
               &QuickBEAM.VM.Runtime.Globals.Constructors.generator_function/2},
            "prototype" => generator_proto,
            {:symbol, "Symbol.toStringTag"} => "GeneratorFunction",
            proto() => Heap.get_func_proto()
          })

        Heap.put_prop_desc(proto_ref(proto), "prototype", %{
          writable: false,
          enumerable: false,
          configurable: false
        })

        Process.put(:qb_generator_function_prototype, proto)
        proto
    end
  end

  defp proto_ref({:obj, ref}), do: ref

  defp callable_prototype({:builtin, _, _} = callable) do
    if Builtin.callable?(callable) do
      callable_static_prototype(callable)
    else
      Heap.get_object_prototype()
    end
  end

  defp callable_prototype(callable), do: callable_static_prototype(callable)

  defp callable_static_prototype(callable) do
    case Map.get(Heap.get_ctor_statics(callable), "__proto__") do
      nil -> Heap.get_func_proto()
      parent -> parent
    end
  end
end
