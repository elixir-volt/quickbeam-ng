defmodule QuickBEAM.VM.Runtime.Globals.Constructors do
  @moduledoc "Global constructor built-ins: `Object`, `Array`, `String`, `Boolean`, and other wrapper constructors."

  import QuickBEAM.VM.Heap.Keys
  import QuickBEAM.VM.Builtin, only: [arg: 3, object: 1]

  alias QuickBEAM.VM.{BytecodeParser, Heap}
  alias QuickBEAM.VM.Interpreter
  alias QuickBEAM.VM.JSThrow
  alias QuickBEAM.VM.Runtime

  @doc "Helper for global constructor built-ins: `object`, `array`, `string`, `boolean`, and other wrapper constructors."
  def object([arg | _], _) do
    case arg do
      {:symbol, _, _} = symbol ->
        ref = make_ref()
        Heap.put_obj(ref, %{"__wrapped_symbol__" => symbol})
        {:obj, ref}

      {:obj, _} = obj ->
        obj

      value when is_binary(value) ->
        ref = make_ref()
        Heap.put_obj(ref, %{"__wrapped_string__" => value})
        {:obj, ref}

      value when is_number(value) ->
        ref = make_ref()
        Heap.put_obj(ref, %{"__wrapped_number__" => value})
        {:obj, ref}

      value when is_boolean(value) ->
        ref = make_ref()
        Heap.put_obj(ref, %{"__wrapped_boolean__" => value})
        {:obj, ref}

      {:bigint, _} = value ->
        ref = make_ref()
        Heap.put_obj(ref, %{"__wrapped_bigint__" => value})
        {:obj, ref}

      _ ->
        Runtime.new_object()
    end
  end

  def object(_, _), do: Runtime.new_object()

  @doc "Helper for global constructor built-ins: `object`, `array`, `string`, `boolean`, and other wrapper constructors."
  def array(args, _) do
    list =
      case args do
        [n] when is_integer(n) and n >= 0 -> List.duplicate(:undefined, n)
        _ -> args
      end

    Heap.wrap(list)
  end

  @doc "Helper for global constructor built-ins: `object`, `array`, `string`, `boolean`, and other wrapper constructors."
  def string(args, {:obj, _} = this) do
    val = args |> arg(0, "") |> Runtime.stringify()
    QuickBEAM.VM.ObjectModel.Put.put(this, "__wrapped_string__", val)
    this
  end

  def string(args, _), do: args |> arg(0, "") |> Runtime.stringify()

  @doc "Helper for global constructor built-ins: `object`, `array`, `string`, `boolean`, and other wrapper constructors."
  def number(args, {:obj, _} = this) do
    val = args |> arg(0, 0) |> Runtime.to_number()
    QuickBEAM.VM.ObjectModel.Put.put(this, "__wrapped_number__", val)
    this
  end

  def number(args, _), do: args |> arg(0, 0) |> Runtime.to_number()

  @doc "Helper for global constructor built-ins: `object`, `array`, `string`, `boolean`, and other wrapper constructors."
  def function(args, _) do
    ctx = Heap.get_ctx()

    if ctx && ctx.runtime_pid do
      {params, body} =
        case Enum.reverse(args) do
          [body | param_parts] ->
            {Enum.join(Enum.reverse(param_parts), ","), body}

          [] ->
            {"", ""}
        end

      code = "(function(" <> params <> "){" <> body <> "})"

      case QuickBEAM.Runtime.compile(ctx.runtime_pid, code) do
        {:ok, bytecode} ->
          case BytecodeParser.decode(bytecode) do
            {:ok, parsed} ->
              case Interpreter.eval(
                     parsed.value,
                     [],
                     %{gas: Runtime.gas_budget(), runtime_pid: ctx.runtime_pid},
                     parsed.atoms
                   ) do
                {:ok, value} -> value
                _ -> JSThrow.syntax_error!("Invalid function")
              end

            _ ->
              JSThrow.syntax_error!("Invalid function")
          end

        _ ->
          JSThrow.syntax_error!("Invalid function")
      end
    else
      JSThrow.error!("Function constructor requires runtime")
    end
  end

  @doc "Helper for global constructor built-ins: `object`, `array`, `string`, `boolean`, and other wrapper constructors."
  def bigint([n | _], _) when is_integer(n), do: {:bigint, n}
  def bigint([{:bigint, n} | _], _), do: {:bigint, n}

  def bigint([string | _], _) when is_binary(string) do
    case Integer.parse(string) do
      {n, ""} -> {:bigint, n}
      _ -> JSThrow.syntax_error!("Cannot convert to BigInt")
    end
  end

  def bigint(_, _) do
    JSThrow.type_error!("Cannot convert to BigInt")
  end

  @doc "Helper for global constructor built-ins: `object`, `array`, `string`, `boolean`, and other wrapper constructors."
  def regexp([], this), do: regexp(["" | []], this)

  def regexp([pattern | rest], _) do
    flags =
      case rest do
        [flag | _] when is_binary(flag) -> flag
        _ -> ""
      end

    source =
      case pattern do
        {:regexp, value, _} -> value
        value when is_binary(value) -> value
        _ -> ""
      end

    {:regexp, source, flags}
  end

  @doc "Helper for global constructor built-ins: `object`, `array`, `string`, `boolean`, and other wrapper constructors."
  def proxy([target, handler | _], _) do
    Heap.wrap(%{proxy_target() => target, proxy_handler() => handler})
  end

  def proxy(_, _), do: Runtime.new_object()

  @doc "Helper for global constructor built-ins: `object`, `array`, `string`, `boolean`, and other wrapper constructors."
  def finalization_registry([_callback | _], _), do: finalization_registry_object()
  def finalization_registry(_, _), do: finalization_registry_object()

  defp finalization_registry_object do
    object do
      method "register" do
        :undefined
      end

      method "unregister" do
        :undefined
      end
    end
  end
end
